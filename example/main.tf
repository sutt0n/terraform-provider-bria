terraform {
  required_providers {
    briaadmin = {
      source  = "galoymoney/briaadmin"
      version = "0.0.7"
    }
    bria = {
      source  = "galoymoney/bria"
      version = "0.0.14"
    }
  }
}

resource "random_string" "postfix" {
  length  = 6
  special = false
  upper   = false
  numeric = false
}

resource "briaadmin_account" "example" {
  name = "tf-example-${random_string.postfix.result}"
}

provider "bria" {
  api_key = briaadmin_account.example.api_key
}

resource "bria_profile" "example" {
  name = "profile-${random_string.postfix.result}"
}

resource "bria_profile" "restricted" {
  name = "restricted-profile-${random_string.postfix.result}"
  spending_policy {
    allowed_payout_addresses = ["mgWUuj1J1N882jmqFxtDepEC73Rr22E9GU"]
    max_payout_sats          = 1000000
  }
}

resource "bria_api_key" "example" {
  profile = bria_profile.example.name
}

output "api_key" {
  value     = bria_api_key.example.key
  sensitive = true
}

resource "bria_xpub" "lnd" {
  name       = "lnd"
  xpub       = "tpubDDEGUyCLufbxAfQruPHkhUcu55UdhXy7otfcEQG4wqYNnMfq9DbHPxWCqpEQQAJUDi8Bq45DjcukdDAXasKJ2G27iLsvpdoEL5nTRy5TJ2B"
  derivation = "m/64h/1h/0"
}

resource "bria_wallet" "wpkh" {
  name = "wpkh"
  keychain {
    wpkh {
      xpub = bria_xpub.lnd.id
    }
  }
}

resource "bria_static_address" "example" {
  external_id = "example"
  wallet      = bria_wallet.wpkh.name
}

output "static_address" {
  value = bria_static_address.example.address
}

resource "bria_wallet" "descriptors" {
  name = "descriptors"
  # private seed tprv8ZgxMBicQKsPf4w53vZs1kfFZcYu3MkxhMhuuEMZPZTGcufQVyEk2PVgiRDQ6qkG7NSsTkYVBFo4YLtv1yHHpqd4aHWmmNVb1kTqNdydjZq
  keychain {
    descriptors {
      external = "wpkh([9f0a3290/84'/0'/0']tpubDDFGc53QkzeuPL7YQe9pv323VrmZhjgkHALNtA1YLgU9j8gmqrDGU1sSNrJRsxdSHF15oQ2Xs83J324cLY4Tqqx5M9wmqRJLedjn6ZEK2S3/0/*)#cxrwymse"
      internal = "wpkh([9f0a3290/84'/0'/0']tpubDDFGc53QkzeuPL7YQe9pv323VrmZhjgkHALNtA1YLgU9j8gmqrDGU1sSNrJRsxdSHF15oQ2Xs83J324cLY4Tqqx5M9wmqRJLedjn6ZEK2S3/1/*)#fjx0ewqp"
    }
  }
}

resource "bria_signer_config" "lnd" {
  xpub = bria_xpub.lnd.id
  lnd {
    endpoint        = "localhost:10009"
    macaroon_base64 = "AgEDbG5kAvgBAwoQB1FdhGa9xoewc1LEXmnURRIBMBoWCgdhZGRyZXNzEgRyZWFkEgV3cml0ZRoTCgRpbmZvEgRyZWFkEgV3cml0ZRoXCghpbnZvaWNlcxIEcmVhZBIFd3JpdGUaIQoIbWFjYXJvb24SCGdlbmVyYXRlEgRyZWFkEgV3cml0ZRoWCgdtZXNzYWdlEgRyZWFkEgV3cml0ZRoXCghvZmZjaGFpbhIEcmVhZBIFd3JpdGUaFgoHb25jaGFpbhIEcmVhZBIFd3JpdGUaFAoFcGVlcnMSBHJlYWQSBXdyaXRlGhgKBnNpZ25lchIIZ2VuZXJhdGUSBHJlYWQAAAYgqHDdwGCqx0aQL1/Z3uUfzCpeBhfapGf9s/AZPOVwf6s="
    cert            = <<EOT
-----BEGIN CERTIFICATE-----
MIICTzCCAfagAwIBAgIRAN7zELSxwC0+P97mtkLTDeMwCgYIKoZIzj0EAwIwODEf
MB0GA1UEChMWbG5kIGF1dG9nZW5lcmF0ZWQgY2VydDEVMBMGA1UEAxMMYWI4NDIz
NGJlMTEzMB4XDTIyMDkyMjA4MjQ0NloXDTM0MDMyNDA4MjQ0NlowODEfMB0GA1UE
ChMWbG5kIGF1dG9nZW5lcmF0ZWQgY2VydDEVMBMGA1UEAxMMYWI4NDIzNGJlMTEz
MFkwEwYHKoZIzj0CAQYIKoZIzj0DAQcDQgAE+fYHYw5VuOsVn7kCqy4dvK99y2OP
0A//zHN52G5Nm6apoyQlvbjeCyVVmz63uit3yIprAXAmv9ca8RPC77XZ+qOB4DCB
3TAOBgNVHQ8BAf8EBAMCAqQwEwYDVR0lBAwwCgYIKwYBBQUHAwEwDwYDVR0TAQH/
BAUwAwEB/zAdBgNVHQ4EFgQUU1Kg1ObzxEmK1rWwkgvQ+lObIiUwgYUGA1UdEQR+
MHyCDGFiODQyMzRiZTExM4IJbG9jYWxob3N0gg1sbmQtb3V0c2lkZS0xgg1sbmQt
b3V0c2lkZS0yggRsbmQxggRsbmQyggR1bml4ggp1bml4cGFja2V0ggdidWZjb25u
hwR/AAABhxAAAAAAAAAAAAAAAAAAAAABhwSsGQAMMAoGCCqGSM49BAMCA0cAMEQC
IHJfxAKakV11FTi0qlZ8/5Z7zn4Rize4UnFEKlHrwcp4AiA59nQlOBqb8RtpCkqd
FvV1D2W0uFGiZLTHgfh4VaHMyA==
-----END CERTIFICATE-----
EOT
  }
}

resource "bria_payout_queue" "interval" {
  name        = "interval-${random_string.postfix.result}"
  description = "An example Bria payout queue"

  config {
    tx_priority                      = "NEXT_BLOCK"
    consolidate_deprecated_keychains = false
    interval_secs                    = 3600
    cpfp_payouts_after_blocks        = 2
    cpfp_payouts_after_mins          = 30
    force_min_change_sats            = 100000
  }
}

resource "bria_payout_queue" "manual" {
  name        = "manual-${random_string.postfix.result}"
  description = "A manual trigger Bria payout queue"

  config {
    tx_priority                      = "NEXT_BLOCK"
    consolidate_deprecated_keychains = false
    manual                           = true
  }
}
